#if defined _clothing_callbacks_
    #endinput
#endif
#define _clothing_callbacks_

public OnGameModeInit()
{
    /* Binco */
    EnterExit_Create(19902, "{ED2B2B}Binco", "{DADADA}Salida", 2244.484863, -1665.22351, 15.476562, 0.0, 0, 0, 207.744949, -111.073318, 1005.132812, 0.0, 1, 15);
    CreateDynamicMapIcon(2244.484863, -1665.22351, 15.476562, 45, -1, .worldid = 0, .interiorid = 0);

    EnterExit_Create(19902, "{ED2B2B}Binco", "{DADADA}Salida", 2102.4263, 2257.575, 11.0234, 273.9107, 0, 0, 207.744949, -111.073318, 1005.132812, 0.0, 2, 15);
    CreateDynamicMapIcon(2102.4263, 2257.575, 11.0234, 45, -1, .worldid = 0, .interiorid = 0);

    EnterExit_Create(19902, "{ED2B2B}Binco", "{DADADA}Salida", 1656.5707, 1733.1418, 10.8281, 91.4768, 0, 0, 207.744949, -111.073318, 1005.132812, 0.0, 3, 15);
    CreateDynamicMapIcon(1656.5707, 1733.1418, 10.8281, 45, -1, .worldid = 0, .interiorid = 0);

    EnterExit_Create(19902, "{ED2B2B}Binco", "{DADADA}Salida", -2374.1768, 910.1226, 45.4317, 88.0863, 0, 0, 207.744949, -111.073318, 1005.132812, 0.0, 4, 15);
    CreateDynamicMapIcon(-2374.1768, 910.1226, 45.4317, 45, -1, .worldid = 0, .interiorid = 0);


    /* SubUrban */
    EnterExit_Create(19902, "{ED2B2B}SubUrban", "{DADADA}Salida", 2112.835205, -1211.456665, 23.962865, 180.0, 0, 0, 203.906326, -50.494247, 1001.804687, 0.0, 1, 1);
    CreateDynamicMapIcon(2112.835205, -1211.456665, 23.962865, 45, -1, .worldid = 0, .interiorid = 0);

    EnterExit_Create(19902, "{ED2B2B}SubUrban", "{DADADA}Salida", 2779.4438, 2453.6345, 11.0625, 133.9879, 0, 0, 203.906326, -50.494247, 1001.804687, 0.0, 2, 1);
    CreateDynamicMapIcon(2779.4438, 2453.6345, 11.0625, 45, -1, .worldid = 0, .interiorid = 0);

    EnterExit_Create(19902, "{ED2B2B}SubUrban", "{DADADA}Salida", -2490.3079, -28.9528, 25.6172, 87.1329, 0, 0, 203.906326, -50.494247, 1001.804687, 0.0, 3, 1);
    CreateDynamicMapIcon(-2490.3079, -28.9528, 25.6172, 45, -1, .worldid = 0, .interiorid = 0);


    /* ProLaps */
    EnterExit_Create(19902, "{ED2B2B}ProLaps", "{DADADA}Salida", 499.498168, -1360.616088, 16.368682, 340.0, 0, 0, 206.995925, -140.021163, 1003.507812, 0.0, 1, 3);
    CreateDynamicMapIcon(499.498168, -1360.616088, 16.368682, 45, -1, .worldid = 0, .interiorid = 0);


    /* Didier Sachs */
    EnterExit_Create(19902, "{ED2B2B}Didier Sachs", "{DADADA}Salida", 453.858032, -1478.098632, 30.813968, 111.110641, 0, 0, 204.348281, -168.678985, 1000.523437, 0.0, 1, 14);
    CreateDynamicMapIcon(453.858032, -1478.098632, 30.813968, 45, -1, .worldid = 0, .interiorid = 0);


    /* Victim */
    EnterExit_Create(19902, "{ED2B2B}Victim", "{DADADA}Salida", 461.54132, -1500.892333, 31.049194, 98.0, 0, 0, 227.35025, -8.188652, 1002.210937, 90.0, 1, 5);
    CreateDynamicMapIcon(461.54132, -1500.892333, 31.049194, 45, -1, .worldid = 0, .interiorid = 0);

    EnterExit_Create(19902, "{ED2B2B}Victim", "{DADADA}Salida", -1694.8934, 951.5375, 24.8906, 138.3028, 0, 0, 227.35025, -8.188652, 1002.210937, 90.0, 2, 5);
    CreateDynamicMapIcon(-1694.8934, 951.5375, 24.8906, 45, -1, .worldid = 0, .interiorid = 0);

    EnterExit_Create(19902, "{ED2B2B}Victim", "{DADADA}Salida", 2802.7219, 2430.3391, 11.0625, 133.3612, 0, 0, 227.35025, -8.188652, 1002.210937, 90.0, 3, 5);
    CreateDynamicMapIcon(2802.7219, 2430.3391, 11.0625, 45, -1, .worldid = 0, .interiorid = 0);


    /* ZIP */
    EnterExit_Create(19902, "{ED2B2B}ZIP", "{DADADA}Salida", 1456.611816, -1137.818969, 23.9614, 215.0, 0, 0, 161.401184, -96.887367, 1001.804687, 0.0, 1, 18);
    CreateDynamicMapIcon(1456.611816, -1137.818969, 23.9614, 45, -1, .worldid = 0, .interiorid = 0);

    EnterExit_Create(19902, "{ED2B2B}ZIP", "{DADADA}Salida", 2571.9072, 1904.4851, 11.0234, 191.6232, 0, 0, 161.401184, -96.887367, 1001.804687, 0.0, 2, 18);
    CreateDynamicMapIcon(2571.9072, 1904.4851, 11.0234, 45, -1, .worldid = 0, .interiorid = 0);

    EnterExit_Create(19902, "{ED2B2B}ZIP", "{DADADA}Salida", 2090.3845, 2224.2493, 11.0234, 179.2598, 0, 0, 161.401184, -96.887367, 1001.804687, 0.0, 3, 18);
    CreateDynamicMapIcon(2090.3845, 2224.2493, 11.0234, 45, -1, .worldid = 0, .interiorid = 0);

    EnterExit_Create(19902, "{ED2B2B}ZIP", "{DADADA}Salida", -1882.4941, 866.1361, 35.1719, 135.4828, 0, 0, 161.401184, -96.887367, 1001.804687, 0.0, 4, 18);
    CreateDynamicMapIcon(-1882.4941, 866.1361, 35.1719, 45, -1, .worldid = 0, .interiorid = 0);

    // Areas
    Clothing_CreateArea(CLOTHING_BINCO, 207.733657, -100.633468, 1005.257812, 15);
    Clothing_CreateArea(CLOTHING_SUBURBAN, 203.905395, -43.450450, 1001.804687, 1);
    Clothing_CreateArea(CLOTHING_PROLAPS, 207.049148, -129.177581, 1003.507812, 3);
    Clothing_CreateArea(CLOTHING_DIDIER_SACHS, 204.348281, -159.493728, 1000.523437, 14);
    Clothing_CreateArea(CLOTHING_VICTIM, 206.374328, -7.241514, 1001.210937, 5);
    Clothing_CreateArea(CLOTHING_ZIP, 161.443634, -83.589271, 1001.804687, 18);

    #if defined CLOTH_OnGameModeInit
        return CLOTH_OnGameModeInit();
    #else
        return 1;
    #endif
}

#if defined _ALS_OnGameModeInit
    #undef OnGameModeInit
#else
    #define _ALS_OnGameModeInit
#endif
#define OnGameModeInit CLOTH_OnGameModeInit
#if defined CLOTH_OnGameModeInit
    forward CLOTH_OnGameModeInit();
#endif

public OnPlayerDisconnect(playerid, reason)
{
    KillTimer(g_rgiRotateSkinTimer[playerid]);

    #if defined CLOTH_OnPlayerDisconnect
        return CLOTH_OnPlayerDisconnect(playerid, reason);
    #else
        return 1;
    #endif
}

#if defined _ALS_OnPlayerDisconnect
    #undef OnPlayerDisconnect
#else
    #define _ALS_OnPlayerDisconnect
#endif
#define OnPlayerDisconnect CLOTH_OnPlayerDisconnect
#if defined CLOTH_OnPlayerDisconnect
    forward CLOTH_OnPlayerDisconnect(playerid, reason);
#endif

forward CLOTH_RotatePlayerSkin(playerid, Float:delta);
public CLOTH_RotatePlayerSkin(playerid, Float:delta)
{
    new Float:angle;
    GetPlayerFacingAngle(playerid, angle);
    angle += delta;
    if (angle < 0.0 || angle > 360.0)
        angle = 0.0;

    SetPlayerFacingAngle(playerid, angle);
    return 1;
}

public OnPlayerKeyStateChange(playerid, newkeys, oldkeys)
{
    if((newkeys & KEY_YES) != 0)
    {
        if(!Bit_Get(Player_Flags(playerid), PFLAG_SHOPPING_CLOTHES))
        {
            if(IsPlayerInAnyDynamicArea(playerid))
            {
                new area[2];
                GetPlayerDynamicAreas(playerid, area);

                for(new i = sizeof(area) - 1; i != -1; --i)
                {
                    new info[2];
                    Streamer_GetArrayData(STREAMER_TYPE_AREA, area[i], E_STREAMER_EXTRA_ID, info);

                    if(info[0] == 0x434c53)
                    {
                        Bit_Set(Player_Flags(playerid), PFLAG_SHOPPING_CLOTHES, true);
                        TogglePlayerControllable(playerid, false);

                        g_rgePlayerTempData[playerid][e_iPlayerLastWorld] = GetPlayerVirtualWorld(playerid);
                        SetPlayerVirtualWorld(playerid, 1 + playerid);

                        g_rgiPlayerClothingStore[playerid] = info[1];

                        new Float:x, Float:y, Float:z, Float:angle;
                        GetPlayerPos(playerid, x, y, z);
                        GetPlayerFacingAngle(playerid, angle);
                        GetXYFromAngle(x, y, angle, 2.0);

                        InterpolateCameraPos(playerid, x, y, z + 1.0, x, y, z + 1.0, 1000);
                        GetPlayerPos(playerid, x, y, z);
                        InterpolateCameraLookAt(playerid, x, y, z + 0.5, x, y, z, 1000);

                        g_rgiRotateSkinTimer[playerid] = SetTimerEx("CLOTH_RotatePlayerSkin", 75, true, "if", playerid, 5.0);

                        for(new j = (sizeof(g_tdShops) - 1); j != -1; --j)
                        {
                            TextDrawShowForPlayer(playerid, g_tdShops[j]);
                        }

                        TextDrawSetStringForPlayer(g_tdShops[5], playerid, "Comprar ropa");

                        SelectTextDraw(playerid, 0xD2B567FF);
                        PlayerPlaySound(playerid, 1145);

                        g_rgiPlayerSelectedSkin[playerid] = 0;
                        Clothing_Select(playerid, 0);
                    }
                }
            }
        }
    }

    #if defined CLOTH_OnPlayerKeyStateChange
        return CLOTH_OnPlayerKeyStateChange(playerid, newkeys, oldkeys);
    #else
        return 1;
    #endif
}

#if defined _ALS_OnPlayerKeyStateChange
    #undef OnPlayerKeyStateChange
#else
    #define _ALS_OnPlayerKeyStateChange
#endif
#define OnPlayerKeyStateChange CLOTH_OnPlayerKeyStateChange
#if defined CLOTH_OnPlayerKeyStateChange
    forward CLOTH_OnPlayerKeyStateChange(playerid, newkeys, oldkeys);
#endif


public OnPlayerClickTextDraw(playerid, Text:clickedid)
{
    if(Bit_Get(Player_Flags(playerid), PFLAG_SHOPPING_CLOTHES))
    {
        // Left button
        if(clickedid == g_tdShops[7])
        {
            --g_rgiPlayerSelectedSkin[playerid];
            if (g_rgiPlayerSelectedSkin[playerid] < 0)
                g_rgiPlayerSelectedSkin[playerid] = 0;

            Clothing_Select(playerid, g_rgiPlayerSelectedSkin[playerid]);
            PlayerPlaySound(playerid, SOUND_BACK);
        }
        // Right button
        else if(clickedid == g_tdShops[8])
        {
            ++g_rgiPlayerSelectedSkin[playerid];
            if (!g_rgiClothingSkins[ g_rgiPlayerClothingStore[playerid] ][ Player_Sex(playerid) ][ g_rgiPlayerSelectedSkin[playerid] ][0])
                g_rgiPlayerSelectedSkin[playerid] = 0;

            Clothing_Select(playerid, g_rgiPlayerSelectedSkin[playerid]);
            PlayerPlaySound(playerid, SOUND_NEXT);
        }
        // Buy button
        else if(clickedid == g_tdShops[9])
        {
            if (Player_Money(playerid) < g_rgiClothingSkins[ g_rgiPlayerClothingStore[playerid] ][ Player_Sex(playerid) ][ g_rgiPlayerSelectedSkin[playerid] ][1])
            {
                PlayerPlaySound(playerid, SOUND_ERROR);
                Notification_ShowBeatingText(playerid, 3000, 0xED2B2B, 100, 255, "No tienes el dinero suficiente.");
                return 0;
            }

            Notification_Show(playerid, "Ropa comprada.", 3000, 0x64A752FF);
            PlayerPlaySound(playerid, 1054);

            mysql_format(g_hDatabase, HYAXE_UNSAFE_HUGE_STRING, HYAXE_UNSAFE_HUGE_LENGTH, "\
                UPDATE `ACCOUNT` SET `SKIN` = %i WHERE `ID` = %i;\
            ", 
                Player_Skin(playerid), Player_AccountID(playerid)
            );
            mysql_tquery(g_hDatabase, HYAXE_UNSAFE_HUGE_STRING);

            Player_GiveMoney(playerid, -g_rgiClothingSkins[ g_rgiPlayerClothingStore[playerid] ][ Player_Sex(playerid) ][ g_rgiPlayerSelectedSkin[playerid] ][1]);
            Player_Skin(playerid) = g_rgiClothingSkins[ g_rgiPlayerClothingStore[playerid] ][ Player_Sex(playerid) ][ g_rgiPlayerSelectedSkin[playerid] ][0];
            SetPlayerSkin(playerid, Player_Skin(playerid));

            Player_StopShopping(playerid);
            SetPlayerVirtualWorld(playerid, g_rgePlayerTempData[playerid][e_iPlayerLastWorld]);
        }
    }

    #if defined CLOTH_OnPlayerClickTextDraw
        return CLOTH_OnPlayerClickTextDraw(playerid, Text:clickedid);
    #else
        return 1;
    #endif
}

#if defined _ALS_OnPlayerClickTextDraw
    #undef OnPlayerClickTextDraw
#else
    #define _ALS_OnPlayerClickTextDraw
#endif
#define OnPlayerClickTextDraw CLOTH_OnPlayerClickTextDraw
#if defined CLOTH_OnPlayerClickTextDraw
    forward CLOTH_OnPlayerClickTextDraw(playerid, Text:clickedid);
#endif


public OnPlayerCancelTDSelection(playerid)
{
    if(Bit_Get(Player_Flags(playerid), PFLAG_SHOPPING_CLOTHES))
    {
        SetPlayerVirtualWorld(playerid, g_rgePlayerTempData[playerid][e_iPlayerLastWorld]);
        Player_StopShopping(playerid);
        SetPlayerSkin(playerid, Player_Skin(playerid));
        return 1;
    }

    #if defined CLOTH_OnPlayerCancelTDSelection
        return CLOTH_OnPlayerCancelTDSelection(playerid);
    #else
        return 1;
    #endif
}

#if defined _ALS_OnPlayerCancelTDSelection
    #undef OnPlayerCancelTDSelection
#else
    #define _ALS_OnPlayerCancelTDSelection
#endif
#define OnPlayerCancelTDSelection CLOTH_OnPlayerCancelTDSelection
#if defined CLOTH_OnPlayerCancelTDSelection
    forward CLOTH_OnPlayerCancelTDSelection(playerid);
#endif
